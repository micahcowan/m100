5 CLS
10 R=1
20 SX=INT(240/2)
30 SY=INT(64/2)
40 RD=0.3
60 LET PI=ATN(1)*4
65 AD=PI/2.005
70 X0=SX:Y0=SY
90 Y1=SY+SIN(A)*R
95 ' IF Y1<0 THEN END ' *****
100 X1=SX+COS(A)*R
105 IF R>SY THEN GOTO 300
110 LINE (X0,Y0) - (X1,Y1)
115 X0=X1:Y0=Y1
130 A=A+AD*5
140 R=R+RD*5
200 GOTO 90
205 END
300 ' check for and handle out-of-bound lines
305 M=(Y1-Y0)/(X1-X0)
310 IF X0>=0 AND X0<240 AND Y0>=0 AND Y0<64 THEN X2=X0:Y2=Y0:GOTO 400
315 ' an initial portion of the line is out of bounds
320 IF Y0>=64 THEN Y2=63:NY=Y2:GOSUB 1000:X2=NX:GOTO 400
330 IF Y0<0 THEN Y2=0:NY=Y2:GOSUB 1000:X2=NX:GOTO 400
340 STOP
395 ' the in-bounds portion of the line
396 ' X2,Y2 is start of in-bounds line
397 ' segment; X3,Y3 is the end of it.
400 IF X1>=0 AND X1<240 AND Y1>=0 AND Y1<64 THEN X3=X1:Y3=Y1:GOTO 500
410 IF Y1>=64 THEN Y3=63:NY=Y3:GOSUB 1000:X3=NX:GOTO 500
420 IF Y1<0 THEN Y3=0:NY=Y3:GOSUB 1000:X3=NX:GOTO 500
495 ' draw the line
500 LINE (X2,Y2)-(X3,Y3)
600 GOTO 115
1000 NX=X0+(NY-Y0)/(Y1-Y0)*(X1-X0)
1010 RETURN
